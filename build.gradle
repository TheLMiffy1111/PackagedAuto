plugins {
	id "com.crystaelix.loom" version "jitpack-afd363f1"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "net.kyori.blossom" version "[1,2)"
	id "me.modmuss50.mod-publish-plugin" version "0.5.0"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.parchmentmc.net/"
			}
		}
		filter {
			includeGroup "org.parchmentmc.data"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
			includeGroup "vazkii.patchouli"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://modmaven.dev/"
			}
		}
		filter {
			includeGroup "appeng"
			includeGroup "mekanism"
		}
	}
}

sourceSets {
	main
}

def mcVersion = "1.19.2"
def modName = "PackagedAuto"
def modId = "packagedauto"
def specVersion = "3"
def vendor = "thelm"

group = "thelm.packagedauto"
version = "3.2.5.20"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(17)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	interfaceInjection.enableDependencyInterfaceInjection = false
	runs {
		configureEach {
			property "fabric.log.console.level", "debug"
		}
		client
		server
	}
}

dependencies {
	// Log4j
	implementation "org.apache.logging.log4j:log4j-core:2.22.1"

	// Minecraft
	minecraft "com.mojang:minecraft:1.19.2"
	mappings loom.layered {
		officialMojangMappings()
		parchment "org.parchmentmc.data:parchment-1.19.2:2022.11.27@zip"
	}
	forge "net.minecraftforge:forge:1.19.2-43.3.8"

	// Dependencies
	modImplementation("mezz.jei:jei-1.19.2-forge:11.6.0.1018") {transitive = false}
	modImplementation "appeng:appliedenergistics2-forge:12.9.8"
	modImplementation "vazkii.patchouli:Patchouli:1.19.2-77"

	// Testing
	modImplementation "curse.maven:cucumber-272335:4773184"
	modImplementation "curse.maven:extended-crafting-268387:4991289"
	modImplementation "mekanism:Mekanism:1.19.2-10.3.9.13"
	modImplementation "curse.maven:applied-mekanistics-574300:4734608"

	// Performance
	modImplementation "curse.maven:yeetusexperimentus-635427:3909789"
	implementation "io.github.llamalad7:mixinextras-forge:0.3.5"
	modImplementation "curse.maven:modernfix-790626:5077300"
	modImplementation "curse.maven:ferritecore-429235:4117906"
	modImplementation "curse.maven:lazy-dfu-forge-460819:4327266"
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

publishMods {
	file = remapJar.archiveFile
	version = displayName = "${mcVersion}-${project.version}"
	changelog = file("changelog.md").text
	type = STABLE
	modLoaders.add("forge")
	additionalFiles.from(remapSourcesJar.archiveFile)

	curseforge {
		accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
		projectId = "308380"
		minecraftVersions.add(mcVersion)
		optional "applied-energistics-2"
	}

	modrinth {
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		projectId = "ugIdhQx4"
		minecraftVersions.add(mcVersion)
		optional "ae2"
	}
}
