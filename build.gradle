plugins {
	id "net.neoforged.gradle" version "[6.0.13,7)"
	id "org.parchmentmc.librarian.forgegradle" version "[1,)"
	id "eclipse"
	id "idea"
	id "maven-publish"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	maven {
		url = "https://maven.minecraftforge.net/"
	}
	maven {
		url = "https://repo.spongepowered.org/maven/"
	}
	maven {
		url = "https://maven.tterrag.com/"
	}
	maven {
		url = "https://maven.blamejared.com/"
	}
	maven {
		url = "https://maven.blakesmods.com/releases/"
	}
	maven {
		url = "https://cursemaven.com/"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		url = "https://modmaven.dev/"
	}
}

sourceSets {
	main
}

def mcVersion = "1.19.2"
def modName = "PackagedAuto"
def modId = "packagedauto"
def specVersion = "3"
def vendor = "thelm"

group = "thelm.packagedauto"
version = "3.2.5.18"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(17)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

minecraft {
	mappings channel: "parchment", version: "2022.11.27-1.19.2"
	generateRunFolders = true
	runs {
		configureEach {
			workingDirectory project.file("run")
			property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
			property "forge.logging.console.level", "debug"
			property "mixin.env.remapRefMap", "true"
			property "mixin.env.refMapRemappingFile", "${buildDir}/createSrgToMcp/output.srg"
			mods {
				"${modId}" {
					source sourceSets.main
				}
			}
		}
		client
		server
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:1.19.2-43.3.5"

	implementation fg.deobf("appeng:appliedenergistics2-forge:12.9.8")
	implementation fg.deobf("mezz.jei:jei-1.19.2-forge:11.6.0.1018")
	implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-forge-1.19.2:10.1.52")
	implementation fg.deobf("vazkii.patchouli:Patchouli:1.19.2-77")
	implementation fg.deobf("com.simibubi.create:create-1.19.2:0.5.1.f-46:all") {
		transitive = false
	}
	implementation fg.deobf("mekanism:Mekanism:1.19.2-10.3.9.13")
	implementation fg.deobf("mekanism:Mekanism:1.19.2-10.3.9.13:generators")
	implementation fg.deobf("com.blakebr0.cucumber:Cucumber:1.19.2-6.0.10")
	implementation fg.deobf("com.blakebr0.extendedcrafting:ExtendedCrafting:1.19.2-5.1.9")

	//CurseMaven
	implementation fg.deobf("curse.maven:applied-mekanistics-574300:4734608")
	implementation fg.deobf("curse.maven:ato-405593:4046926")

	//Performance
	implementation fg.deobf("curse.maven:yeetusexperimentus-635427:3909789")
	implementation fg.deobf("curse.maven:modernfix-790626:4980941")
	implementation fg.deobf("curse.maven:ferritecore-429235:4117906")
	implementation fg.deobf("curse.maven:lazy-dfu-forge-460819:4327266")
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor,
	"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
]

jar {
	manifest.attributes(manifestAttributes)
	finalizedBy("reobfJar")
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}
