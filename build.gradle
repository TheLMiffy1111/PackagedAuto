plugins {
	id "com.crystaelix.loom" version "jitpack-a0afe362"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "net.kyori.blossom" version "[1,2)"
	id "me.modmuss50.mod-publish-plugin" version "0.5.0"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
			includeGroup "vazkii.patchouli"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.amadornes.com/"
			}
		}
		filter {
			includeGroup "MCMultiPart2"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://repo.cleanroommc.com/releases/"
			}
		}
		filter {
			includeGroup "zone.rong"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
}

sourceSets {
	main
}

def mcVersion = "1.12.2"
def modName = "PackagedAuto"
def modId = "packagedauto"
def specVersion = "1"
def vendor = "thelm"

group = "thelm.packagedauto"
version = "1.0.10.37"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(8)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	interfaceInjection.enableDependencyInterfaceInjection = false
	runs {
		configureEach {
			property "fabric.log.console.level", "debug"
			environmentVariable "MC_VERSION", mcVersion
		}
		client {
			programArg "--username=Dev"
		}
		server
	}
	generatedIntermediateMappings()
}

dependencies {
	// Log4j
	implementation "org.apache.logging.log4j:log4j-core:2.22.1"

	// Minecraft
	minecraft "com.mojang:minecraft:1.12.2"
	mappings "de.oceanlabs.mcp:mcp_stable:39-1.12"
	legacyForge "net.minecraftforge:forge:1.12.2-14.23.5.2860"

	// Dependencies
	modImplementation "mezz.jei:jei_1.12.2:4.16.1.1012"
	modImplementation "curse.maven:applied-energistics-2-223794:2747063"
	modImplementation "vazkii.patchouli:Patchouli:1.0-23.6"

	// Testing
	modImplementation "curse.maven:cucumber-272335:2645867"
	modImplementation "curse.maven:extended-crafting-268387:2777071"
	modImplementation "MCMultiPart2:MCMultiPart:2.5.3"
	modImplementation "curse.maven:mekanism-268560:2835175"

	// Performance
	implementation("zone.rong:mixinbooter:9.1") {transitive = false}
	modImplementation "curse.maven:reid-629017:4949840"
	modImplementation "curse.maven:vintagefix-871198:5010926"
	modImplementation "curse.maven:lolasm-460609:4800875"
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

publishMods {
	file = remapJar.archiveFile
	version = displayName = "${mcVersion}-${project.version}"
	changelog = file("changelog.md").text
	type = STABLE
	modLoaders.add("forge")
	additionalFiles.from(remapSourcesJar.archiveFile)

	curseforge {
		accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
		projectId = "308380"
		minecraftVersions.add(mcVersion)
		javaVersions.add(JavaVersion.VERSION_1_8)
		optional "applied-energistics-2"
	}

	modrinth {
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		projectId = "ugIdhQx4"
		minecraftVersions.add(mcVersion)
		optional "ae2"
	}
}
