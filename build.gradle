plugins {
	id "net.neoforged.gradle" version "[6.0.13,7)"
	id "wtf.gofancy.fancygradle" version "[1.1,)"
	id "eclipse"
	id "idea"
	id "maven-publish"
}

apply from: "mixinTweakFix.gradle"

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	maven {
		url = "https://maven.minecraftforge.net/"
	}
	maven {
		url = "https://repo.spongepowered.org/maven/"
	}
	maven {
		url = "https://maven.su5ed.dev/releases/"
	}
	maven {
		url = "https://maven.blamejared.com/"
	}
	maven {
		url = "https://maven.covers1624.net/"
	}
	maven {
		url = "https://maven.blakesmods.com/releases/"
		metadataSources {
			artifact()
		}
	}
	maven {
		url = "https://maven.cleanroommc.com/"
	}
	maven {
		url = "https://cursemaven.com/"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		url = "https://modmaven.dev/"
	}
}

sourceSets {
	main
}

def mcVersion = "1.12.2"
def modName = "PackagedAuto"
def modId = "packagedauto"
def specVersion = "1"
def vendor = "thelm"

group = "thelm.packagedauto"
version = "1.0.10.36"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(8)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

minecraft {
	mappings channel: "stable", version: "39-1.12"
	generateRunFolders = true
	runs {
		configureEach {
			workingDirectory project.file("run")
			property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
			property "forge.logging.console.level", "debug"
			property "mixin.env.remapRefMap", "true"
			property "mixin.env.refMapRemappingFile", "${buildDir}/createSrgToMcp/output.srg"
			environment "MC_VERSION", mcVersion
			mods {
				"${modId}" {
					source sourceSets.main
				}
			}
		}
		client {
			arg "--username=Dev"
		}
		server
	}
}

fancyGradle {
	patches {
		resources
		coremods
		codeChickenLib
		asm
		mergetool
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2859"

	implementation fg.deobf("mezz.jei:jei_1.12.2:4.16.1.302")
	implementation fg.deobf("codechicken:CodeChickenLib:1.12.2-3.2.3.358:universal")
	implementation fg.deobf("CraftTweaker2:ZenScript:4.1.20.697")
	implementation fg.deobf("CraftTweaker2:CraftTweaker2-API:4.1.20.697")
	implementation fg.deobf("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.697")
	implementation fg.deobf("morph.avaritia:Avaritia:1.12.2-3.3.0.33:universal") {
		transitive = false
	}
	implementation fg.deobf("com.blakebr0.cucumber:Cucumber:1.12.2-1.1.3")
	implementation fg.deobf("com.blakebr0.extendedcrafting:ExtendedCrafting:1.12.2-1.5.6")

	//CurseMaven
	implementation fg.deobf("curse.maven:applied-energistics-2-223794:2747063")
	implementation fg.deobf("curse.maven:redstone-flux-270789:2920436")
	implementation fg.deobf("curse.maven:cofh-core-69162:2920433")
	implementation fg.deobf("curse.maven:cofh-world-271384:2920434")
	implementation fg.deobf("curse.maven:thermal-foundation-222880:2926428")
	implementation fg.deobf("curse.maven:thermal-expansion-69163:2926431")
	implementation fg.deobf("curse.maven:thermal-dynamics-227443:2920505")

	//Performance
	implementation fg.deobf("curse.maven:reid-629017:4949840")
	implementation("zone.rong:mixinbooter:7.1") {
		transitive = false
	}
	implementation fg.deobf("curse.maven:vintagefix-871198:4800687")
	implementation fg.deobf("curse.maven:lolasm-460609:4800875")
}

configurations.configureEach {
	resolutionStrategy {
		force "net.minecraftforge:legacydev:0.2.4.1"
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor,
	"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
]

jar {
	manifest.attributes(manifestAttributes)
	finalizedBy("reobfJar")
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}
