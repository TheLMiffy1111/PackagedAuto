plugins {
	id "com.crystaelix.loom" version "jitpack-a0afe362"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "cl.franciscosolis.blossom-extended" version "[1,2)"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.parchmentmc.net/"
			}
		}
		filter {
			includeGroup "org.parchmentmc.data"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
			includeGroup "vazkii.patchouli"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.tterrag.com/"
			}
		}
		filter {
			includeGroup "com.jozufozu.flywheel"
			includeGroup "com.simibubi.create"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://modmaven.dev/"
			}
		}
		filter {
			includeGroup "appeng"
			includeGroup "mekanism"
		}
	}
}

sourceSets {
	main
}

def mcVersion = "1.16.5"
def modName = "PackagedAuto"
def modId = "packagedauto"
def specVersion = "2"
def vendor = "thelm"

group = "thelm.packagedauto"
version = "2.0.5.26"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(8)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	interfaceInjection.enableDependencyInterfaceInjection = false
	runs {
		configureEach {
			property "fabric.log.console.level", "debug"
		}
		client
		server
	}
}

dependencies {
	// Log4j
	implementation "org.apache.logging.log4j:log4j-core:2.22.1"

	// Minecraft
	minecraft "com.mojang:minecraft:1.16.5"
	mappings loom.layered {
		mappings("de.oceanlabs.mcp:mcp_config:1.16.5-20210115.111550@zip") {
			mappingPath "config/joined.tsrg"
			fallbackNamespaces "official", "named"
			mergeNamespace "official"
		}
		officialMojangMappings {
			skipClassNames = true
		}
		parchment "org.parchmentmc.data:parchment-1.16.5:2022.03.06@zip"
	}
	forge "net.minecraftforge:forge:1.16.5-36.2.39"

	// Dependencies
	modImplementation "mezz.jei:jei-1.16.5:7.8.0.1009"
	modImplementation "appeng:appliedenergistics2:8.4.7"
	modImplementation "vazkii.patchouli:Patchouli:1.16.4-53.3"

	// Testing
	modImplementation "curse.maven:cucumber-272335:3507886"
	modImplementation "curse.maven:extended-crafting-268387:4430385"
	modImplementation "com.jozufozu.flywheel:Flywheel:1.16-0.2.5.53"
	modImplementation "com.simibubi.create:Create:mc1.16.5_v0.3.2g+480"
	modImplementation "mekanism:Mekanism:1.16.5-10.1.2.457"

	// Performance
	modImplementation "curse.maven:shutup-experimental-settings-407174:3188120"
	modImplementation "curse.maven:modernfix-790626:5077294"
	modImplementation "curse.maven:ferritecore-429235:3435176"
	modImplementation "curse.maven:lazy-dfu-forge-460819:3249059"
}

blossom {
	replaceToken "0@VERSION@", version
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}
