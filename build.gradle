plugins {
	id "net.neoforged.gradle" version "[6.0.13,7)"
	id "org.parchmentmc.librarian.forgegradle" version "[1,)"
	id "eclipse"
	id "idea"
	id "maven-publish"
}

repositories {
	jcenter()
	flatDir {
		dirs "libs"
	}
	maven {
		url = "https://repo.spongepowered.org/maven/"
	}
	maven {
		url = "https://maven.tterrag.com/"
	}
	maven {
		url = "https://maven.blamejared.com/"
	}
	maven {
		url = "https://maven.blakesmods.com/releases/"
	}
	maven {
		url = "https://cursemaven.com/"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		url = "https://modmaven.dev/"
	}
}

sourceSets {
	main
}

def mcVersion = "1.20.1"
def modName = "PackagedAuto"
def modId = "packagedauto"
def specVersion = "3"
def vendor = "thelm"

group = "thelm.packagedauto"
version = "3.4.2.7"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(17)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

minecraft {
	mappings channel: "parchment", version: "2023.07.30-1.20.1"
	generateRunFolders = true
	runs {
		configureEach {
			workingDirectory project.file("run")
			property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
			property "forge.logging.console.level", "debug"
			property "mixin.env.remapRefMap", "true"
			property "mixin.env.refMapRemappingFile", "${buildDir}/createSrgToMcp/output.srg"
			mods {
				"${modId}" {
					source sourceSets.main
				}
			}
		}
		client
		server
	}
}

dependencies {
	minecraft "net.neoforged:forge:1.20.1-47.1.79"

	implementation fg.deobf("appeng:appliedenergistics2-forge:15.0.11")
	implementation fg.deobf("mezz.jei:jei-1.20.1-forge:15.2.0.27")
	implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-forge-1.20.1:14.0.17") {
		transitive = false
	}
	implementation fg.deobf("vazkii.patchouli:Patchouli:1.20.1-81-FORGE")
	implementation fg.deobf("com.simibubi.create:create-1.20.1:0.5.1.e-23:all") {
		transitive = false
	}
	implementation fg.deobf("mekanism:Mekanism:1.20.1-10.4.2.16")
	implementation fg.deobf("mekanism:Mekanism:1.20.1-10.4.2.16:generators")
	implementation fg.deobf("com.blakebr0.cucumber:Cucumber:1.20.1-7.0.1")
	implementation fg.deobf("com.blakebr0.extendedcrafting:ExtendedCrafting:1.20.1-6.0.2")

	//CurseMaven
	implementation fg.deobf("curse.maven:applied-mekanistics-574300:4776865")
	implementation fg.deobf("curse.maven:ato-405593:4751952")
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor,
	"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
]

jar {
	manifest.attributes(manifestAttributes)
	finalizedBy("reobfJar")
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}
